allprojects {
    apply plugin: 'maven'
    group = 'org.fl.opm'
    version = '1.0.6'

    task deploy(dependsOn: 'uploadArchives') {
    }

    task release(dependsOn: 'deploy') {
    }

    gradle.taskGraph.whenReady {taskGraph ->
        if (!taskGraph.hasTask(release)) {
            version += '-SNAPSHOT'
        }
    }
}

subprojects {
    apply plugin: 'java'
    compileJava.options.encoding = 'UTF-8'

    configurations {
        providedCompile
    }

    repositories {
        mavenLocal()
        mavenCentral()
        mavenRepo url: "http://nexus.shengpaydev.com/content/groups/public"
    }

    dependencies {
        testCompile "junit:junit:4.11"
    }

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    sourceSets {
        main.compileClasspath += configurations.providedCompile
        test.compileClasspath += configurations.providedCompile
        test.runtimeClasspath += configurations.providedCompile
        println sourceSets.main
    }

    task srcJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.java
        from sourceSets.main.resources
    }

    artifacts {
        archives srcJar
    }

    uploadArchives {
        repositories.mavenDeployer {
            snapshotRepository(url: "http://nexus.shengpaydev.com/content/repositories/snapshots/") {
                authentication(userName: "dev", password: "sdpdev")
            }
            repository(url: "http://nexus.shengpaydev.com/content/repositories/releases/") {
                authentication(userName: "dev", password: "sdpdev")
            }
        }
    }

}
